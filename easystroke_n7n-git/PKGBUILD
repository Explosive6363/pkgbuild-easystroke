# Maintainer:  Jordan Johnston <johnstonljordan AT gmail DOT com>
# Contributor: AppleBloom <rat.o.drat@gmail.com>

_pkgname="easystroke_n7n"
pkgname="$_pkgname-git"
pkgver=0.6.0.r43.g4d2bd1c
pkgrel=1
pkgdesc="Gesture-recognition application for X11."
arch=("i686" "x86_64")
url="https://github.com/thjaeger/easystroke/wiki"
license=("custom")
depends=("gtkmm3" "boost-libs" "libxtst" "dbus-glib" "xorg-server" "cmake")
makedepends=("boost" "xorgproto" "intltool" "gettext" "xorg-server-devel" "help2man" "git" "vala")
provides=("$pkgname" "easystroke" "easystroke-git")
conflicts=("$pkgname" "easystroke" "easystroke-git")
install="$_pkgname.install"
source=("git+https://github.com/nine7nine/easystroke.git")
md5sums=('SKIP')

pkgver() {
  cd "easystroke"
  git describe --long | sed "s/\([^-]*-g\)/r\1/;s/-/./g"
}

build() {
  cd "easystroke"
   
  cmake .
  make
}

package() {
  cd "easystroke"
  make PREFIX="/usr" DESTDIR="$pkgdir" install
  install -Dm644 "easystroke.desktop" "$pkgdir/usr/share/applications/easystroke.desktop"
  install -Dm644 "easystroke.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/easystroke.svg"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$_pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:
